plugins {
    id 'java'
    id 'application'
    id 'org.beryx.runtime' version '1.12.7'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    manifest {
        attributes("Main-Class": project.main_class)
    }
}

application {
    mainClass = project.main_class
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.desktop', 'java.scripting', 'jdk.dynalink', 'java.xml', 'java.datatransfer', 'java.sql',
               'java.logging', 'jdk.crypto.cryptoki', 'jdk.crypto.ec', 'jdk.crypto.mscapi']
}

tasks.jpackageImage.doLast {
    delete("$buildDir/jpackage/${project.name}/scratch")
    delete("$buildDir/jpackage/${project.name}/scratch_ex")
    copy {
        from('scratch')
        into("$buildDir/jpackage/${project.name}/scratch")
    }
    copy {
        from('scratch_ex')
        into("$buildDir/jpackage/${project.name}/scratch_ex")
    }
}

group 'adudecalledleo'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:22.0.0'
    implementation "com.google.code.gson:gson:${project.gson_version}"
    implementation "de.skuzzle:semantic-version:${project.semver_version}"
    implementation "org.commonmark:commonmark:${project.commonmark_version}"
    implementation "org.commonmark:commonmark-ext-autolink:${project.commonmark_version}"
    implementation "org.commonmark:commonmark-ext-gfm-strikethrough:${project.commonmark_version}"
    implementation "org.openjdk.nashorn:nashorn-core:${project.nashorn_version}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

processResources {
    inputs.property "version", project.version

    filesMatching("build_info.json") {
        expand "version": project.version
    }
}

test {
    useJUnitPlatform()
}