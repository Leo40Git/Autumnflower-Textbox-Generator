plugins {
    id 'java'
    id 'application'
    id 'org.beryx.runtime' version '1.12.7'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    manifest {
        attributes("Main-Class": project.main_class)
    }
}

application {
    mainClass = project.main_class
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.desktop', 'java.scripting', 'jdk.dynalink', 'java.xml', 'java.datatransfer', 'java.sql',
               'java.logging', 'jdk.crypto.cryptoki', 'jdk.crypto.ec', 'jdk.crypto.mscapi', 'jdk.unsupported']
}

group 'adudecalledleo'

sourceSets {
    dev {
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

repositories {
    mavenCentral()
    maven {
        name = "QuiltMC Maven"
        url = "https://maven.quiltmc.org/repository/release"
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation "org.apache.logging.log4j:log4j-core:${project.log4j_version}"
    implementation "org.quiltmc:quilt-json5:${project.qjson5_version}"
    implementation "de.skuzzle:semantic-version:${project.semver_version}"
    implementation "org.commonmark:commonmark:${project.commonmark_version}"
    implementation "org.commonmark:commonmark-ext-autolink:${project.commonmark_version}"
    implementation "org.commonmark:commonmark-ext-gfm-strikethrough:${project.commonmark_version}"
    implementation "org.openjdk.nashorn:nashorn-core:${project.nashorn_version}"

    devRuntimeOnly "org.fusesource.jansi:jansi:${project.jansi_version}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

processResources {
    inputs.property "version", project.version

    filesMatching("build_info.json") {
        expand "version": project.version
    }
}

test {
    useJUnitPlatform()
}